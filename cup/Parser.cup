package Example;

import java_cup.runtime.*;

parser code {:
	public static void main(String args[]) throws Exception {
		SymbolFactory sf = new DefaultSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
	}
:}

terminal COMMA;
terminal ARRAY_BEGIN, ARRAY_END;
terminal MINUS;
terminal String DIGITS, FRAC, EXP;
terminal String STRING;
terminal Boolean BOOLEAN;
terminal NULL;

non terminal array, value_list, value;
non terminal Number number;
non terminal String int_value;

array ::= ARRAY_BEGIN ARRAY_END |
          ARRAY_BEGIN value_list ARRAY_END;
value_list ::= value_list COMMA value | value;

value ::= number:e  {: System.out.println(" value: "+e); :} |
          BOOLEAN:b {: System.out.println(" value: "+b); :} |
          STRING:s  {: System.out.println(" value: "+s); :} |
          NULL:n    {: System.out.println(" value: "+n); :};
number ::= int_value:s {: RESULT=new Integer(s); :} |
           int_value:i EXP:e {: RESULT=new Double(i+e); :} |
           int_value:i FRAC:f {: RESULT=new Double(i+f); :} |
           int_value:i FRAC:f EXP:e {: RESULT=new Double(i+f+e); :};
int_value ::= DIGITS:s {: RESULT=s; :} |
              MINUS DIGITS:s {: RESULT="-"+s; :};
