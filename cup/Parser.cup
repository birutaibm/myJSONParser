package Example;

import java_cup.runtime.*;

parser code {:
	public static void main(String args[]) throws Exception {
		SymbolFactory sf = new DefaultSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
	}
:}

terminal COMMA;
terminal ARRAY_BEGIN, ARRAY_END;
terminal MINUS;
terminal Double NUMBER;
terminal String STRING;
terminal Boolean BOOLEAN;
terminal NULL;
terminal OBJ_BEGIN, OBJ_END;
terminal PAIR_SEPARATOR;

non terminal array, elements, value;
non terminal object;
non terminal object_member, pair;
non terminal Number number;

array ::= ARRAY_BEGIN ARRAY_END |
          ARRAY_BEGIN elements ARRAY_END;
elements ::= elements COMMA value | value;
object ::= OBJ_BEGIN OBJ_END |
           OBJ_BEGIN object_member OBJ_END;
object_member ::= pair |
                  pair COMMA object_member;
pair ::= STRING PAIR_SEPARATOR value;
value ::= number:e  {: System.out.println(" value: "+e); :} |
          BOOLEAN:b {: System.out.println(" value: "+b); :} |
          STRING:s  {: System.out.println(" value: "+s); :} |
          array     {: System.out.println(" value: an array"); :} |
          object    {: System.out.println(" value: an object"); :} |
          NULL:n    {: System.out.println(" value: "+n); :};
number ::= NUMBER:n {: RESULT=n; :} |
           MINUS NUMBER:n {: RESULT=-n; :};
