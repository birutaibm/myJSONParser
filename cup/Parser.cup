package Example;

import java_cup.runtime.*;
import java.util.ArrayList;
import java.util.AbstractMap.SimpleImmutableEntry;

parser code {:
	public static void main(String args[]) throws Exception {
		SymbolFactory sf = new DefaultSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
	}
:}

terminal COMMA;
terminal ARRAY_BEGIN, ARRAY_END;
terminal MINUS;
terminal Double NUMBER;
terminal String STRING;
terminal Boolean BOOLEAN;
terminal NULL;
terminal OBJ_BEGIN, OBJ_END;
terminal PAIR_SEPARATOR;

non terminal ArrayList array, elements;
non terminal ArrayList object, object_member;
non terminal SimpleImmutableEntry pair;
non terminal Object value;
non terminal Number number;

value ::= number:e  {: RESULT=e; :} |
          BOOLEAN:b {: RESULT=b; :} |
          STRING:s  {: RESULT=s; :} |
          array     {: RESULT="an array"; :} |
          object    {: RESULT="an object"; :} |
          NULL:n    {: RESULT=n; :};
object ::= OBJ_BEGIN OBJ_END {: RESULT=new ArrayList(); :} |
           OBJ_BEGIN object_member:m OBJ_END {: RESULT=new ArrayList(); RESULT.addAll(m); :};
object_member ::= pair:p {: RESULT=new ArrayList(); RESULT.add(p); :} |
                  pair:p COMMA object_member:m {: RESULT=new ArrayList(); RESULT.add(p); RESULT.addAll(m); :};
pair ::= STRING:k PAIR_SEPARATOR value:v {: RESULT=new SimpleImmutableEntry(k,v); :};
array ::= ARRAY_BEGIN ARRAY_END {: RESULT=new ArrayList(); :} |
          ARRAY_BEGIN elements:e ARRAY_END{: RESULT=new ArrayList(); RESULT.addAll(e); :};
elements ::= elements:e COMMA value:v {: RESULT=new ArrayList(); RESULT.addAll(e); RESULT.add(v); :} |
             value:v {: RESULT=new ArrayList(); RESULT.add(v); :};
number ::= NUMBER:n {: RESULT=n; :} |
           MINUS NUMBER:n {: RESULT=-n; :};
